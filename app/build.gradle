plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.jacoco)
}

android {
    namespace = "com.grapevineindustries.scoretrackertdd"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.grapevineindustries.scoretrackertdd"
        minSdk = 27
        targetSdk = 35
        versionCode = 13
        versionName = "0.8"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.compose.compiler.get()
    }
    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            enableAndroidTestCoverage = true
            enableUnitTestCoverage = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        viewBinding = true
    }

//    testOptions {
//        unitTests {
//            includeAndroidResources = true
//        }
//    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                it.jvmArgs("-noverify")
//                testLogging {
//                    jacoco {
//                        includeNoLocationClasses = true
//                        excludes = ['jdk.internal.*']
//                    }
//                }
            }
        }
    }
//    testOptions {
//        unitTests {
//            IncludeAndroidResources = true
//            all {
//                it.jvmArgs("-noverify")
//            }
//        }
//    }

}

dependencies {
    implementation(platform(libs.compose.bom))
    implementation(project(":plunder"))
    implementation(project(":fivecrowns"))
    implementation(project(":common"))

    implementation(libs.core.ktx)
    implementation(libs.material)
    implementation(libs.material.icons.core)
    implementation(libs.appcompat)
    implementation(libs.material3)
    implementation(libs.constraintlayout)
    implementation(libs.navigation.fragment.ktx)
    implementation(libs.navigation.ui.ktx)
    implementation(libs.activity.compose)
    implementation(libs.navigation.compose)
    implementation(libs.lifecycle.viewmodel.compose)
    implementation(libs.constraintlayout.compose)
    implementation(libs.compose.ui.tooling.preview)
    implementation(libs.kotlinx.serialization.json)

    testImplementation(libs.compose.ui.test.junit4.android)
    testImplementation(libs.espresso.core)
    testImplementation(libs.junit)
    testImplementation(libs.robolectric)
    testImplementation(testFixtures(project(":fivecrowns")))

    debugImplementation(libs.compose.ui.test.manifest)
    debugImplementation(libs.compose.ui.tooling)
}
// Source - https://stackoverflow.com/a
// Posted by Shubhendra Singh
// Retrieved 2026-01-08, License - CC BY-SA 4.0

tasks.register("jacocoFullCoverageReportAllModules", JacocoReport) {
    group = "Reports"
    description = "Generate JaCoCo coverage reports (Unit + Instrumented) for all modules"

    def fileFilter = [
            // Android-specific generated files
            "**/R.class",
            '**/R$*.class',
            "**/BuildConfig.*",
            "**/Manifest*.*",
            "**/resources/**",
            "**/values/**",

            // Test files
            "**/*Test*.*",
//            "**/*Test$*.*",
            "**/androidTest/**",
            "**/test/**",

            // Hilt/Dagger-generated code
            "**/hilt_aggregated_deps/**",
            "**/dagger/hilt/internal/**",
            "**/dagger/hilt/android/internal/**",
            "**/*_MembersInjector.class",
            "**/Dagger*Component.class",
            "**/*Module_*Factory.class",
            "**/*_Factory.class",
            "**/*_Provide*Factory.class",
            "**/*_Impl.class",

            // Kotlin-generated classes
            "**/*\$Lambda*.*",
            "**/*\$inlined*.*",
            "**/*\$*.*", // anonymous classes and lambdas
            "**/Companion.class",

            // Navigation safe args (generated)
            "**/*Directions*.class",
            "**/*Args.class",

            // Jetpack Compose compiler-generated
            "**/*Preview*.*",
            "**/*ComposableSingletons*.*",

            // Room and other annotation processors
            "**/*_Impl.class",
            "**/*Serializer.class", // For Moshi, Retrofit, etc.

            // Miscellaneous
            "android/**/*.*",

            // Project-specific exclusions
            "**/di/**",
            "**/state/**",
            "**/mapper/**",
            "**/domain/**"
    ]

    def javaClasses = []
    def kotlinClasses = []
    def javaSrc = []
    def kotlinSrc = []
    def execution = []

    rootProject.subprojects.each { proj ->
        def unitTestTask = proj.tasks.findByName("testDebugUnitTest")
        if (unitTestTask) {
            dependsOn(unitTestTask)
        }
        def connectedTestTask = proj.tasks.findByName("connectedDebugAndroidTest")
        if (connectedTestTask) {
            dependsOn(connectedTestTask)
        }

        javaClasses.add(proj.fileTree(dir: "${proj.buildDir}/intermediates/javac/debug", excludes: fileFilter))
        kotlinClasses.add(proj.fileTree(dir: "${proj.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter))

        javaSrc.add("${proj.projectDir}/src/main/java")
        kotlinSrc.add("${proj.projectDir}/src/main/kotlin")

        execution.add(proj.fileTree(dir: proj.buildDir, includes: [
                "jacoco/testDebugUnitTest.exec", // Unit test
                "outputs/code_coverage/debugAndroidTest/connected/**/*.ec" // UI test
        ]))
    }

    sourceDirectories.setFrom(files(javaSrc + kotlinSrc))
    classDirectories.setFrom(files(javaClasses + kotlinClasses))
    executionData.setFrom(files(execution))

    reports {
        xml.required.set(true)
        html.required.set(true)
        html.outputLocation.set(file("${rootProject.buildDir}/coverage-report"))
    }

    doLast {
        println("âœ… Combined coverage report generated at:")
        println("ðŸ“„ file://${reports.html.outputLocation.get()}/index.html")
    }
}

// Source - https://stackoverflow.com/a
// Posted by Shubhendra Singh
// Retrieved 2026-01-08, License - CC BY-SA 4.0

tasks.register("runAllCoverageAndReport") {
    group = "Verification"
    description = "Runs unit + UI tests across all modules and generates a full Jacoco report"

    def testTaskPaths = []

    rootProject.subprojects.each { proj ->
        proj.tasks.matching { it.name == "testDebugUnitTest" }.each {
            testTaskPaths.add(it.path)
        }
        proj.tasks.matching { it.name == "createDebugCoverageReport" }.each {
            testTaskPaths.add(it.path)
        }
    }

    dependsOn(testTaskPaths)
    dependsOn("jacocoFullCoverageReportAllModules")

    doFirst {
        println("Running test tasks:")
        testTaskPaths.each { println(" - $it") }
    }
}
